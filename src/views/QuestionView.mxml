<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" 
		xmlns:mx="library://ns.adobe.com/flex/mx" 
		xmlns:BIP="components" 
		title="Lesson #{data.lessonName}"
		contentCreationComplete="contentCreationCompleted(event)" 
		xmlns:components="components.*"
		backgroundColor="0xcccccc"
		>

	
	<fx:Style>
		.myHeadingStyle { 
			fontSize: 24;
			fontWeight: bold;
		}
		
		.questionContentStyle { 
			fontSize: 20;
		}
		
		.questionNoBorderContainer {
			alpha	: 0;
			borderVisible : false;
		}
		
		.questionShowBorderContainer {
			borderVisible : true;
		}
		
		.checkButtonStyle {
			cornerRadius : 10;
			fontSize : 30;
			color : #FFFFFF;
		}
	</fx:Style>

	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<s:Fade id="fadeOut" 
				target="{this}"
				alphaFrom="1"
				alphaTo=".5"
				/>
		<s:Fade id="fadeIn" 
				target="{this}"
				alphaFrom=".5"
				alphaTo="1"
				/>
	</fx:Declarations>
	
	<s:states>
		<s:State name="default" />
		<s:State name="goToQuestion" />
		<s:State name="showAnswer" />
	</s:states>
	<s:transitions>
		<s:Transition fromState="default" toState="goToQuestion" >
				<s:Fade target="{viewContainer}" duration="1000" alphaFrom="1" alphaTo="0"  /> 
		</s:Transition>
		<s:Transition fromState="goToQuestion" toState="default">
			<s:Sequence>
				<!--<s:Fade target="{viewContainer}" duration="2000" alphaFrom="1" alphaTo="0" repeatBehavior="reverse" /> -->
				<s:Fade target="{viewContainer}" duration="1500" alphaFrom="0" alphaTo="1" />
			</s:Sequence>
		</s:Transition>
	</s:transitions>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import components.BIPTitleWindow;
			import components.Currency;
			import components.DragDropView;
			import components.EqualitySymbols;
			import components.Equation;
			import components.ImageMultipleChoice;
			import components.ImperialMeasure;
			import components.MathWords;
			import components.MultipleChoice;
			import components.MyAlertPopUp;
			import components.NumericDisplay;
			import components.OperatorSymbols;
			import components.RewardImage;
			import components.ShapeNames;
			import components.Time;
			
			import events.CloseTitleWindow;
			import events.NumberPadEvent;
			
			
			protected var correctAnswerId : String;
			protected var correctAnswerStr : String;
			protected var wrongAnswerSounds :Array = new Array("error.mp3", "error1.mp3", "wrong-action.mp3", "HoustonProblem.mp3");
			protected var currentQuestionXML : XML;
			protected var currentQuestionType : String; 	// the type of the currently selected question
			protected var rewardTimer : Timer;
			protected var rewardView : RewardImage;
			
			protected var operatorSymbols : OperatorSymbols;
			protected var equalitySymbols : EqualitySymbols;
			protected var timeInput : Time;
			protected var measurement : ImperialMeasure;
			protected var currency : Currency;
			protected var shapeNames : ShapeNames;
			protected var multipleChoice : MultipleChoice;
			protected var equation : Equation;
			protected var dragDrop : DragDropView;
			protected var mathWords : MathWords;
			protected var multipleChoiceImage : ImageMultipleChoice;
			protected var numericDisplay : NumericDisplay;

			
			public var alertPopUp:MyAlertPopUp = new MyAlertPopUp();
			public var numberCorrectAnswers : uint;

			
			private var titleWindow:BIPTitleWindow;
			
			[Bindable]
			public var currentQuestion:Number;
			
			[Bindable]
			public var questionContentStr:String;
			
			[Bindable]
			public var imageUrlContents:String;
			
			[Bindable]
			public var statusStr:String;
			
			
			protected function contentCreationCompleted(event:FlexEvent):void
			{
				currentQuestion = 0;
				trace("lessonName: " + data.lessonName);
				updateQuestionInfo();
				statusStr = "0  / " + data.questions.length();
				var x:Number = Capabilities.screenResolutionX;
				var y:Number = Capabilities.screenResolutionY;
				
//				mainSection.width = (x > y) ? x : y;
//				trace("set width to : " + stage.width);
				trace("stage.scaleMode: " + stage.scaleMode);
				stage.scaleMode = StageScaleMode.NO_SCALE;
				trace("stage.scaleMode: " + stage.scaleMode);
				trace("stage.width: " + stage.width + " stage.stageWidth: " + stage.stageWidth);
				stage.stageHeight = 750;
				stage.stageWidth = 1134;
				stage.addEventListener(StageOrientationEvent.ORIENTATION_CHANGE, 
					onOrientationChange); 
			}
			
			
			protected function checkButtonHandler(event:MouseEvent):void
			{
				if (checkAnswer()) {
					goToNextQuestion(true);
				}
			}
			
			protected function nextButtonHandler(event:MouseEvent):void
			{
				goToNextQuestion(false);
			}
			
			protected function goToNextQuestion(displayTransition:Boolean = false):void
			{
				if (displayTransition)
					currentState = "goToQuestion";
				goToQuestion(++currentQuestion);
				if (displayTransition)
					currentState = "default";
			}
			
			protected function previousButtonHandler(event:MouseEvent):void
			{ 	
				if (currentQuestion == 0) {		// go back to lessons if they click Previous when we are at the first question
					navigator.popToFirstView();
				} else 
					goToQuestion(--currentQuestion);
			}
			
			protected function goToQuestion(questionNumber:uint):void
			{
				if (questionNumber < data.questions.length()) {
					currentQuestion = questionNumber;
					updateQuestionInfo();
				} else {	// go back to the list of lessons
					navigator.popToFirstView();
				}
			}
			
			protected function lessonsButton_clickHandler(event:MouseEvent):void
			{
				navigator.popView();
			}
		
			
			protected function rightSelection():void
			{
				displayRewardImage();
				playCorrectSound();
				numberCorrectAnswers++;
				statusStr = numberCorrectAnswers.toString() + " / " + data.questions.length();
			}
			
			// when question is answered correctly display character image with an affirmation 
			protected function displayRewardImage():void
			{
				if (rewardView == null)
					rewardView = new RewardImage();
				addElement(rewardView);
				if (rewardTimer == null) {
					rewardTimer = new Timer(1000, 2);
					rewardTimer.addEventListener("timer", timerHandler);
				}
				rewardTimer.start();
			}
			
			protected function timerHandler(event:TimerEvent):void {
				trace("timerHandler: " + event);
				rewardTimer.stop();
				if (rewardView)
					removeElement(rewardView);
			}
			
			protected function wrongSelection(correctAnswer:String):void
			{
				trace("Bummer you missed the question...");
				var message:String = "The correct answer is: " + correctAnswer;
				showTitleWindow(message);
				playWrongSound();
			}
			
			protected function playCorrectSound():void{
				playSound("sounds/cityrocker__thats-correct.mp3"); 
			}
			
			
			protected function playWrongSound():void{
				var length:uint = wrongAnswerSounds.length;
				var randomUint:uint = uint(Math.random() * length);
				var randomSound:String = "sounds/" + wrongAnswerSounds[randomUint];
				trace("Error Sound: " + randomSound);
				playSound(randomSound); 
			}
			
			protected function playSound(url:String):void{
				var req:URLRequest = new URLRequest(url); 
				var s:Sound = new Sound(req); 
				s.play();
			}
			
			protected function dragDropContentCreationComplete(event:FlexEvent):void
			{
				dragDrop.setChoices(currentQuestionXML);
			}	
			
			protected function updateQuestionType(question:XML):void
			{
				
				//currentQuestionType = question.@type;
				currentQuestionType = question.type;
				trace("type: " + currentQuestionType);
				var answerXMLList:XMLList = currentQuestionXML.answer;
				var answerContents:String = answerXMLList[0];
				
				if (currentQuestionType == "symbolOperators") {
					operatorSymbols = new OperatorSymbols();
					addToView(operatorSymbols);
				} else if (currentQuestionType == "equality") {
					equalitySymbols = new EqualitySymbols();
					equalitySymbols.addXMLData(currentQuestionXML);
					addToView(equalitySymbols);
				}  else if (currentQuestionType == "time") {
					timeInput = new Time();
					addToView(timeInput);
				}  else if (currentQuestionType == "measurementImperial") {
					measurement = new ImperialMeasure();
					measurement.questionXML = currentQuestionXML;
					addToView(measurement);
				}  else if (currentQuestionType == "currency") {
					currency = new Currency();
					addToView(currency);
				}  else if (currentQuestionType == "shapeNames") {
					shapeNames = new ShapeNames();
					shapeNames.setQuestionXML(currentQuestionXML);
					addToView(shapeNames);
				} else if (currentQuestionType == "equation") {
					equation = new Equation();
					equation.addXMLData(currentQuestionXML);
					addToView(equation);
				}  else if (currentQuestionType == "dragDrop") {
					dragDrop = new DragDropView();
					dragDrop.addEventListener(FlexEvent.CONTENT_CREATION_COMPLETE,dragDropContentCreationComplete);
					addToView(dragDrop);
					dragDrop.setAnswer(answerContents);
				} else if (currentQuestionType == "mathWordsAddSub") {
					mathWords = new MathWords();
					mathWords.setQuestionXML(currentQuestionXML);
					addToView(mathWords);
				} else if (currentQuestionType == "multipleChoice") {
					multipleChoice = new MultipleChoice();
					multipleChoice.setQuestionXML(currentQuestionXML);
					addToView(multipleChoice);
				} else if (currentQuestionType == "multipleChoiceImage") {
					multipleChoiceImage = new ImageMultipleChoice();
					multipleChoiceImage.questionXML = currentQuestionXML;
					addToView(multipleChoiceImage);
				} else {
					numericDisplay = new NumericDisplay();
					addToView(numericDisplay);
					numberPad.addEventListener(NumberPadEvent.EVENT_UPDATE,numericDisplay.onNumberPadUpdate);
				}
				if (displayNumericKeypad(currentQuestionType)) {
					numberPad.clearDisplay();
					numberPad.visible = true;
				} else {
					numberPad.visible = false;
				}
			}
			
			protected function addToView(answerToAdd:*,removeQuestionContents:Boolean=false):void
			{
				answerSection.removeAllElements();	// remove any items that had been previously added
				answerSection.addElement(answerToAdd);
				if (removeQuestionContents) {
					
				}
			}
			
			
			private function showTitleWindow(message:String):void {
				// Create a non-modal TitleWindow container.
				fadeOut.end();
				fadeOut.play();
				titleWindow = PopUpManager.createPopUp(this, BIPTitleWindow, true) as BIPTitleWindow;
//				titleWindow.addEventListener(CloseEvent.CLOSE,closeTitleWindowHandler);
				titleWindow.addEventListener(CloseTitleWindow.EVENT_NAME, closeTitleWindowHandler);
				titleWindow.title = "Error";
				titleWindow.setMessage(message);
				PopUpManager.centerPopUp(titleWindow);
			}

			// invoked when the user closes out the Title Window Modal Dialog e.g. for displaying answers
			protected function closeTitleWindowHandler(event:Event):void
			{
				trace("Close Title Window");
				fadeIn.end();
				fadeIn.play();
				goToNextQuestion();
			}

			// returns true if this answer type contains the question content otherwise we add it separately from the answer
			private function answerContainsQuestionContent(answerType:String):Boolean 
			{
				switch (answerType) {
					case "equation" :
					case "mathWordsAddSub" :
					case "equality" :
						return true;
					default:
						return false;
				}
				return false;
			}
			
			// returns true if this answer type should display the numeric keypad
			private function displayNumericKeypad(answerType:String):Boolean 
			{
				switch (answerType) {
					case "equation" :
					case "numeric" :
					case "time" :
						
					case "measurementImperial" :
					case "currency" :
					case "time" :
						return true;
					default:
						return false;
				}
				return false;
			}
			
			protected function updateQuestionInfo():void 
			{
				currentQuestionXML = data.questions[currentQuestion];
				if (currentQuestionXML) {
					updateQuestionType(currentQuestionXML);
					if (answerContainsQuestionContent(currentQuestionType)) {
						questionContentStr = "";
					} else {
						questionContentStr = currentQuestionXML.content;
					}
					displayImage();
				}
			}
			
			// If there is an <image><imagePath>images/image.png</imagePath></image> node then display the image 
			
			// TO DO Only handles displaying a single image
			private function displayImage():void
			{
				var imagePathList : XMLList = currentQuestionXML.image.imagePath;
				if (imagePathList.length()) {
					var imageUrlStr : String = imagePathList[0].toString();
					trace("image URL: " + imageUrlStr);
					// display the image
					if (imageUrlStr.length > 0) {
						questionImage.visible = true;
						loadImage(imageUrlStr);
						questionImage.maxHeight=300;
						questionImage.includeInLayout = true;
						return;
					}
				} 
				// found no image, make the text take up all of the space
				questionImage.maxHeight=0;
				questionImage.visible = false;
				questionImage.includeInLayout = false;
			}
			
			private function checkAnswer():Boolean
			{

				var answerNode:XMLList;
				var answerContents:String;
				var userAnswer:String;
				answerNode = currentQuestionXML.answer;
				answerContents = answerNode[0];
				if (currentQuestionType== "numeric") {
					userAnswer = numericDisplay.getText();
				} else if (currentQuestionType== "symbolOperators") {
					userAnswer = operatorSymbols.getText();
				} else if (currentQuestionType== "equality") {
					userAnswer = equalitySymbols.getText();
					answerContents = equalitySymbols.getAnswer();
				} else if (currentQuestionType== "time") {
					userAnswer = timeInput.getText();
				} else if (currentQuestionType== "measurementImperial") {
					userAnswer = measurement.response;
					answerContents = measurement.answer;
				} else if (currentQuestionType== "currency") {
					userAnswer = currency.getText();
				} else if (currentQuestionType== "shapeNames") {
					userAnswer = shapeNames.response;
					answerContents = shapeNames.answer;
				} else if (currentQuestionType== "multipleChoice") {
					userAnswer = multipleChoice.response;
					answerContents = multipleChoice.answer;
				} else if (currentQuestionType== "equation") {
					userAnswer = equation.response;
					answerContents = equation.correctAnswer;
				} else if (currentQuestionType== "dragDrop") {
					userAnswer = dragDrop.getAnswer();
				}  else if (currentQuestionType == "mathWordsAddSub") {
					userAnswer = mathWords.getText();
					answerContents = mathWords.answer;
				}  else if (currentQuestionType == "multipleChoiceImage") {
					userAnswer = multipleChoiceImage.getText();
					answerContents = multipleChoiceImage.getAnswer();
				} else {
					trace("ERROR: Don't know how to get ANSWER!!!");
				}
				trace("The correct answer is: " + answerContents + " you entered: " + userAnswer);
				if (answerContents == userAnswer) {
					rightSelection();
					return true;
				} else {
					wrongSelection(answerContents);
					return false;
				}
			}
			
			private function loadImage(filePath:String):void
			{
				var loader:Loader = new Loader();
				loader.contentLoaderInfo.addEventListener(Event.COMPLETE,
					handleLoadComplete);
				/*				loader.load( new URLRequest(
				'http://covers.oreilly.com/images/9780596529857/bkt.gif' ) ); */
				
				loader.load( new URLRequest(
					filePath ) );
			}
			
			private function handleLoadComplete( evt:Event ):void
			{
				var bmp:Bitmap = ( evt.target as LoaderInfo ).content as Bitmap;
				questionImage.source = bmp;
			}
			
			protected function jumpToQuestionButton_clickHandler(event:MouseEvent):void
			{
				var questionToGoTo : String = jumpToQuestionTextInput.text;
				goToQuestion(uint(questionToGoTo) - 1);
			}
			
			
			protected function onOrientationChange(event:StageOrientationEvent):void
			{
				trace("onOrientationChange: " + event.afterOrientation);
			}
			
		]]>
	</fx:Script>

	<s:navigationContent>
		<s:Button label="Lessons" click="navigator.popToFirstView();"/>
	</s:navigationContent>
	
	<s:VGroup id="mainSection" paddingTop="15" paddingBottom="15" paddingLeft="15" paddingRight="15" horizontalAlign="center" width="100%">
		

		<s:HGroup id="statusSection" width="100%">
			<mx:Spacer width="100%" />
			<s:Label text="{statusStr}" paddingRight="10"/>
		</s:HGroup>
		<s:VGroup id="viewContainer" height="100%" minHeight="400" minWidth="500" horizontalAlign="center">
			
			<s:Group width="100%" >
				<s:Label id="questionNumber" text="Question #{currentQuestion+1}" paddingTop="10" horizontalCenter="0" styleName="myHeadingStyle"  />	
			</s:Group>
			
			<mx:Spacer height="15%"/>
			
			<s:VGroup width="100%" horizontalAlign="center">
				<!-- <s:BorderContainer styleName="questionNoBorderContainer"> -->
					<s:Label id="questionContent" text="{questionContentStr}"  styleName="questionContentStyle" paddingRight="10" maxWidth="700" />
				<!-- </s:BorderContainer> -->
				<s:BitmapImage  id="questionImage" source="" fillMode="scale" scaleMode="letterbox" maxWidth="400" maxHeight="300"  />
			</s:VGroup>
			<mx:Spacer height="10%"/>
			<s:HGroup id="answerSection" width="100%" gap="40" horizontalAlign="center">	
			</s:HGroup>

			<mx:Spacer height="10%"/>
		</s:VGroup>
	
	</s:VGroup>
	<s:HGroup width="100%" horizontalAlign="center" bottom="20">
		<s:VGroup width="500" horizontalAlign="center">
			<s:HGroup>
				<s:Button label="Check" id="checkButton" click="checkButtonHandler(event)" skinClass="skins.CheckButtonSkin" styleName="checkButtonStyle" width="480" height="60"/>
			</s:HGroup>
			<s:HGroup width="100%" paddingTop="20" verticalAlign="middle">
				<s:Button label="Lessons" id="backToLessonsButton" click="lessonsButton_clickHandler(event)" />
				<s:Button label="&lt; Previous" id="previousButton" click="previousButtonHandler(event)" icon="@Embed('icons/Previous.png')" />
				<s:Button label="Next >" click="nextButtonHandler(event)" icon="@Embed('icons/Next.png')"  />
				<mx:Spacer width="100%" />
				<s:TextInput id="jumpToQuestionTextInput" width="40" />
				<s:Button id="jumpToQuestionButton" label="Go" click="jumpToQuestionButton_clickHandler(event)" />
				<mx:Spacer width="100%"/>
			</s:HGroup>
			
		</s:VGroup>
	</s:HGroup>
	<components:NumberPad id="numberPad" right="10" visible="false">
	</components:NumberPad>

</s:View>
